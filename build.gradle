plugins {
    id "dev.architectury.loom" version "1.11-SNAPSHOT" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = project.maven_group
    version = project.mod_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {

        // Cobblemon
        maven { url = "https://maven.impactdev.net/repository/development/" }

        // CurseMaven (RCT / RCT API)
        maven {
            name = "CurseMaven"
            url = "https://www.cursemaven.com"
            content { includeGroup "curse.maven" }
        }
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }


        // Architectury (por si algo lo requiere como dependencia normal)
        maven { url = "https://maven.architectury.dev/" }

        mavenCentral()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }


}
